#version 460

#extension GL_NV_ray_tracing : require

layout(binding = 0, set = 0) uniform accelerationStructureNV AS;
layout(binding = 1, set = 0, rgba8) uniform writeonly image2D shadowTexture;
layout(binding = 2, set = 0) uniform sampler2D depthTexture;

struct Payload {
    vec3 value;
};

layout(location = 0) rayPayloadNV Payload payload;

layout(push_constant) uniform pushConstants {
    vec3 light_direction;
    mat4 inverseViewProjection;
} pc;

vec3 worldPosFromDepth(vec2 uv, float depth) {
    vec4 clipPos = vec4(uv * 2.0 - vec2(1.0), 2.0 * depth - 1.0, 1.0);
    vec4 worldPos = pc.inverseViewProjection * clipPos;
    return(worldPos.xyz / worldPos.w);
}

void main() {
    const vec2 pixelCenter = vec2(gl_LaunchIDNV.xy) + vec2(0.5);
    const vec2 uv = pixelCenter/vec2(gl_LaunchSizeNV.xy);

    uint rayFlags = gl_RayFlagsOpaqueNV;
    float tMin = 0.001;
    float tMax = 10000.0;

    float depth = texture(depthTexture, uv).r;

    if(depth == 1.0) return;

    vec3 origin = worldPosFromDepth(uv, depth);

    vec3 direction = normalize(-pc.light_direction);

    payload.value = vec3(0);

    traceNV(AS, rayFlags, 0xFF, 0, 0, 0, origin, tMin, direction, tMax, 0);

    imageStore(shadowTexture, ivec2(gl_LaunchIDNV.xy), vec4(payload.value, 1.0));
}